initialize() {
	i=readFile("~/scratch/index.txt"); //Index to get the next Seed (checked if file with other seed exsist with R)
	i=asInteger(i);
	setSeed(i);
	burnIn=readFile("~/scratch/BurnIn.txt");
	burnIn=asInteger(burnIn);
	burnIn=ifelse(burnIn==0,F,T);
	
	initializeSLiMOptions(keepPedigrees=T); // keep Pedigrees for relatedness function
	
	//length of chromosomes
	c_length = c(2490, 2422, 1983, 1902, 1815, 1708, 1593, 1451, 1384, 1338, 1351, 1333, 1144, 1070, 1020, 903, 833, 803, 586, 644, 467, 508) * 1e5;
	
	//vector ends (endpoints of chromosomes)
	ends = cumSum(c_length);
	
	// m1 mutationtype: neutral
	initializeMutationType("m1", 0.5, "f", 0.0);
	
	// m2 mutationtype: deletirious
	initializeMutationType("m2", 0.0, "f", -1.0);
	
	// g1 -> gene, g2 -> no gene
	initializeGenomicElementType("g1", c(1,2), c(8,1));
	initializeGenomicElementType("g2", 2, 0); //no gene
	
	m1.color = "pink";
	m2.color = "green";
	g1.color="blue";
	g2.color="#D0F0FF";
	
	start=readFile("~/scratch/start.txt"); //vector with startpionts of the genes and the non-coding parts
	start=asInteger(start);
	defineConstant("START", start);
	
	end=readFile("~/scratch/end.txt"); //vector with endpoints
	end=asInteger(end);
	defineConstant("END", end);
	
	genomicType=readFile("~/scratch/genomicType.txt"); //vector with genomic types 
	genomicType=asInteger(genomicType);
	
	mutationRate=readFile("~/scratch/mutationRate.txt"); //vector with mutationsrates 
	mutationRate=asFloat(mutationRate);
	
	// make genomic types with specified mutationrates
	for(i in seqAlong(genomicType)){
		initializeGenomicElement(genomicType[i],start[i],end[i]);
	}
	initializeMutationRate(mutationRate,end);
	
	
	rates=rep(1e-8,22);
	initializeRecombinationRate(rates,ends);
	
	//define constant
	defineConstant("N0", 500); //start populationsize
	defineConstant("lambda", 2.05);
	defineConstant("simID", getSeed());
	defineConstant("BurnIn", burnIn);
	
	
	//two sexes
	initializeSex("A");
}

// make population with N0 individuals
1 {
	sim.addSubpop("p1", N0);
}


//------------------------------------------------------------------------------------------------------------------
//calculate number of mutationload and ill individuals, and save the data after every generation
//------------------------------------------------------------------------------------------------------------------
1 early(){ //header of table
	writeFile("~/scratch/randomResults/random_populationsize_mutationload_prevalence_burnIn" + BurnIn + "_" + simID + ".txt", "generation,populationsize,mutationload,ill individuals", append=T);
}

1:350 {
	muts=sum(p1.individuals.countOfMutationsOfType(m2)); //number of all mutations
	mutationload=muts/p1.individualCount; //mutationload
	ills=p1.individualCount-p1.individualCount*mean(p1.cachedFitness(NULL)); //ill individuals
	writeFile("~/scratch/randomResults/random_populationsize_mutationload_prevalence_burnIn" + BurnIn + "_" + simID + ".txt", (sim.generation +","+p1.individualCount+","+mutationload+","+ills),append=T); //save
}


//------------------------------------------------------------------------------------------------------------------
//population growth and mateChoice
//------------------------------------------------------------------------------------------------------------------

//new populationsize
1:350 {
	female = p1.individuals[p1.individuals.sex=="F"];
	female = female[female.fitnessScaling==1];
	if(BurnIn==T & sim.generation<=200){
		newSize = sum(rpois(length(female), N0/length(female)));
	}
	else{newSize = sum(rpois(length(female), lambda));}
	p1.setSubpopulationSize(newSize);
}

//------------------------------------------------------------------------------------------------------------------
//compound-heterozygotie
//------------------------------------------------------------------------------------------------------------------

1:350 late() {
	
	// Loop over individuals and figure out the fitness of each one holistically
	for (ind in p1.individuals)
	{
		// get the positions of all m2 mutations in each of the two genomes
		g1pos = ind.genome1.mutationsOfType(m2).position;
		g2pos = ind.genome2.mutationsOfType(m2).position;
		
		if ((length(g1pos) > 0) & (length(g2pos) > 0)){ // in both genomes are mutations (m2)
			
			// use sapply() to find the index of the gene for each position
			g1elem = sapply(g1pos, "whichMax(applyValue <= END);");
			g2elem = sapply(g2pos, "whichMax(applyValue <= END);");
			
			// unique down the gene indexes since multiple mutations in the same gene are irrelevant
			g1elem = unique(g1elem);
			g2elem = unique(g2elem);
			
			// find the intersection of the gene indexes hit on the two genomes
			// which genes have in both genomes the mutation m2
			elemsHitInBoth = setIntersection(g1elem, g2elem);
			
			// if a gene was hit on both genomes, fitness is 0.0 (1.0 is the default)
			if (size(elemsHitInBoth) > 0)
				ind.fitnessScaling = 0.0;
		}
	}
}
